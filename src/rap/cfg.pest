string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
	( "\"\"" ) |
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

ident = @{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

property = { ident ~ equals ~ (string | number) ~ semicolon }

array_elemn = {
    bracket_open ~ bracket_close |
    bracket_open ~ (string | number) ~ (comma ~ (string | number))* ~ bracket_close
}

array = { ident  ~ array_equals ~ array_elemn ~ semicolon }

// Keywords/Symbols

array_equals = _{ "[" ~ "]" ~ "=" }
equals = _{ "=" }
semicolon = _{ ";"  }
class_keyword = _{ "class" }
delete_keyword = _{ "delete" }
bracket_open = _{ "{" }
bracket_close = _{ "}" }
comma = _{ "," }
colon = _{ ":" }

class = { 
	class_keyword ~ ident ~ (colon ~ ident)? ~ bracket_open ~ 
	(property | class | array | deleted_class | extern_class)* ~
	bracket_close ~ semicolon 
}

deleted_class = {
    delete_keyword ~ ident ~ semicolon
}

extern_class = {
    class_keyword ~ ident ~ semicolon
}
    
cfg = _{ SOI ~ (property | class | array | deleted_class | extern_class)* ~ EOI }
